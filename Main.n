#pragma indent

using Nemerle.Extensions
using Nemerle.Text

using System
using System.Console
using System.Diagnostics
using System.IO
using System.IO.Compression
using System.Linq
using System.Net
using System.Reflection
using System.Text
using System.Text.RegularExpressions

module Program
  private Here = Assembly.GetExecutingAssembly().Location |> Path.GetDirectoryName
  
  Main() : void
    def download(url)
      using (wc = WebClient() <- BaseAddress = "http://www.sqlite.org")
        wc.DownloadData(url)
    
    def fetchExecutable()
      def throwsNotFound = () =>
        throw FileNotFoundException("An archive file is not found in download page.")
        Environment.Exit(1)
      def page = download("download.html") |> Encoding.UTF8.GetString
      // exeファイル入りzipのURLを取得
      match (Regex.Match(page, <#\('a\d+?','(?<href>\d+/sqlite\-tools\-win32\-x86(?:\-\d+?)?\.zip)'\)#>))
        | m when m != null =>
          def temp = Path.GetTempFileName()
          try
            // byte配列データをダウンロードしてテンポラリファイルに出力
            Write("downloading...")
            download(m.Groups["href"].Value) |> File.WriteAllBytes(temp, _)
            WriteLine(" done.")
            
            using (archive = ZipFile.OpenRead(temp))
              foreach (entry in archive.Entries) regexp match (entry.Name)
                // 実行ファイルを展開
                | @"(?<fname>sqlite\d*\.exe)" =>
                  Write("extracting...")
                  Path.Combine(Here, fname) |> entry.ExtractToFile
                  WriteLine(" done.")
                | _ => ()
              otherwise throwsNotFound()
          finally
            File.Delete(temp)
        | _ => throwsNotFound()
    
    def getSqlite()
      def files = Directory.EnumerateFiles(Here)
      files.FirstOrDefault <| Regex.IsMatch(_, @"sqlite\d*\.exe") ??
        // 無かったらネットから取ってくる
        fetchExecutable()
        getSqlite()
    
    def sqlite = getSqlite()
    def compact(path)
      Write($"compacting $(path |> Path.GetFileName)...")
      try
        // コンパクション実行
        def psi = ProcessStartInfo(sqlite, $"$path vacuum") <-
          UseShellExecute = false;
          CreateNoWindow = true;
        def proc = Process() <- StartInfo = psi
        _ = proc.Start()
        proc.WaitForExit()
        WriteLine(" done.")
      catch
        | _ => WriteLine(" failed.")
    
    def extAcceptable = s => Path.GetExtension(s) |> [".db", ".db3"].Contains
    def files = Directory.EnumerateFiles(Here, "*", SearchOption.AllDirectories)
    foreach (path when extAcceptable(path) in files)
      compact(path)
    